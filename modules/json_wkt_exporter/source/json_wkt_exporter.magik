def_slotted_exemplar(
	:json_wkt_exporter,
	{
		{:collections, _unset, :readable},
		{:json_writer, _unset, :readable},
		{:wkt_serializer, _unset, :readable},
		{:comma_pending?, _false, :readable}
	},
	{})
$

_pragma(classify_level=basic, topic={json_wkt_exporter})
_method json_wkt_exporter.new(p_filename, p_collections, p_source_coordinate_system, p_target_coordinate_system, p_epsg_number)
	#  Initial Author  : steven.looman, Date : 2011-11-21
	## Parameters      : -
	## Returns         : -
	## Pre             : -
	## Function        : -
	## Post            : -
	_return _clone.init(p_filename, p_collections, p_source_coordinate_system, p_target_coordinate_system, p_epsg_number)
_endmethod
$

_pragma(classify_level=restricted, topic={json_wkt_exporter})
_private _method json_wkt_exporter.init(p_filename, p_collections, p_source_coordinate_system, p_target_coordinate_system, p_epsg_number)
	#  Initial Author  : steven.looman, Date : 2011-11-21
	## Parameters      : -
	## Returns         : -
	## Pre             : -
	## Function        : -
	## Post            : -
	.json_writer << json_writer.new(p_filename)
	.collections << p_collections
	.wkt_serializer << wkt_serializer.new(p_source_coordinate_system, p_target_coordinate_system, p_epsg_number)
	_return _self
_endmethod
$

_pragma(classify_level=basic, topic={json_wkt_exporter})
_method json_wkt_exporter.run()
	#  Initial Author  : steven.looman, Date : 2011-11-21
	## Parameters      : -
	## Returns         : -
	## Pre             : -
	## Function        : -
	## Post            : -
	_dynamic !print_float_precision! << 10

	_self.json_writer.json_start_object()
	_for i_collection _over .collections.fast_elements()
	_loop
		_self.json_writer.json_start_element(i_collection.name)
		_self.json_writer.json_start_array()
		_self.export_collection(i_collection)
		_self.json_writer.json_end_array()
		_self.json_writer.json_end_element() # i_collection.name
	_endloop
	_self.json_writer.json_end_object()
_endmethod
$


_pragma(classify_level=restricted, topic={json_wkt_exporter})
_private _method json_wkt_exporter.export_collection(p_collection)
	#  Initial Author  : steven.looman, Date : 2011-11-21
	## Parameters      : -
	## Returns         : -
	## Pre             : -
	## Function        : -
	## Post            : -
	_for i_record _over p_collection.fast_elements()
	_loop
		_local l_source_collection << i_record.source_collection
		_local l_source_collection_name << l_source_collection.name
		_local l_record_id << _self.key_value_as_string(i_record)

		_self.json_writer.begin_object()
		_for i_field _over i_record.visible_fields.fast_elements()
		_loop
			_self.export_field(i_record, i_field)
		_endloop
		_self.json_writer.end_object()
	_endloop
_endmethod
$

_pragma(classify_level=restricted, topic={json_wkt_exporter})
_private _method json_wkt_exporter.key_value_as_string(p_record)
	#  Initial Author  : steven.looman, Date : 2011-11-22
	## Parameters      : -
	## Returns         : -
	## Pre             : -
	## Function        : -
	## Post            : -
	_local l_key_string << ""
	_local l_key << p_record.key_value()
	_for i_key_value _over l_key.fast_elements()
	_loop
		l_key_string << write_string(l_key_string, i_key_value, ",")
	_endloop
	_if _not l_key_string.empty?
	_then
		_local l_total_size << l_key_string.size
		l_key_string << l_key_string.subseq(1, l_total_size - 1)
	_endif
	_return l_key_string
_endmethod
$

_pragma(classify_level=restricted, topic={json_wkt_exporter})
_private _method json_wkt_exporter.export_field(p_record, p_field)
	#  Initial Author  : steven.looman, Date : 2011-11-21
	## Parameters      : -
	## Returns         : -
	## Pre             : -
	## Function        : -
	## Post            : -
	_local l_field_value << p_record.perform(p_field.name)
	_if l_field_value _is _unset
	_then
		# nothing
	_elif p_field.is_geometry?
	_then
		_self.export_geometry_field(p_field, l_field_value)
	_elif p_field.is_physical?
	_then
		_self.export_physical_field(p_field, l_field_value)
	_endif
	# XXX: TODO: is_join?
	# XXX: TODO: is_derived?
_endmethod
$

_pragma(classify_level=restricted, topic={json_wkt_exporter})
_private _method json_wkt_exporter.export_geometry_field(p_field, p_field_value)
	#  Initial Author  : steven.looman, Date : 2011-11-21
	## Parameters      : -
	## Returns         : -
	## Pre             : -
	## Function        : -
	## Post            : -
	_self.json_writer.begin_pair(p_field.name)
	_local l_wkt_string << _self.wkt_serializer.export_geometry(p_field_value)
	_self.json_writer.write_value(l_wkt_string)
_endmethod
$

_pragma(classify_level=restricted, topic={json_wkt_exporter})
_private _method json_wkt_exporter.export_physical_field(p_field, p_field_value)
	#  Initial Author  : steven.looman, Date : 2011-11-21
	## Parameters      : -
	## Returns         : -
	## Pre             : -
	## Function        : -
	## Post            : -
	_self.json_writer.begin_pair(p_field.name)
	_self.json_writer.write_value(p_field_value)
_endmethod
$
